apply plugin: 'signing'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.nhaarman'
version = rootProject.ext.versionName
def sonatypeUsername = hasProperty('sonatype_username') ? sonatype_username : System.getenv('SONATYPE_USERNAME')
def sonatypePassword = hasProperty('sonatype_password') ? sonatype_password : System.getenv('SONATYPE_PASSWORD')


task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from 'build/javadoc'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name 'AsyncAwait-Android'
                packaging 'aar'
                description 'Async-Await in Android'
                url 'https://github.com/nhaarman/asyncawait-android'

                scm {
                    url 'scm:git@github.com:nhaarman/asyncawait-android.git'
                    connection 'scm:git@github.com:nhaarman/asyncawait-android.git'
                    developerConnection 'scm:git@github.com:nhaarman/asyncawait-android.git'
                }

                licenses {
                    license {
                        name 'Apache 2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'nhaarman'
                        name 'Niek Haarman'
                    }
                }
            }
        }
    }
}

bintray {
    user = hasProperty('bintray_username') ? bintray_username : System.getenv('BINTRAY_USER')
    key = hasProperty('bintray_key') ? bintray_key : System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = 'AsyncAwait-Android'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nhaarman/AsyncAwait-Android.git'

        version {
            name = versionName

            gpg {
                sign = true
            }

            mavenCentralSync {
                sync = true
                user = hasProperty('sonatype_username') ? sonatype_username : System.getenv('SONATYPE_USERNAME')
                password = hasProperty('sonatype_password') ? sonatype_password : System.getenv('SONATYPE_PASSWORD')
                close = '1'
            }
        }
    }
}